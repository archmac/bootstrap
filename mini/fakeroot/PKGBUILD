pkgname=fakeroot
pkgver=1.25.3
pkgrel=2
pkgdesc='Tool for simulating superuser privileges'
arch=('x86_64')
source=(https://salsa.debian.org/clint/fakeroot/-/archive/24d6b0857396cad87b2cabd32fb8af9ef4799915/fakeroot-24d6b0857396cad87b2cabd32fb8af9ef4799915.tar.bz2)

prepare() {
  cd fakeroot-24d6b0857396cad87b2cabd32fb8af9ef4799915

  patch -p0 <<'PATCH'
--- configure.ac	2021-05-16 13:10:01.000000000 +0200
+++ -	2021-05-16 13:10:04.000000000 +0200
@@ -604,8 +604,6 @@
 AC_CONFIG_FILES([
    Makefile
    scripts/Makefile
-   doc/Makefile
-   doc/de/Makefile doc/es/Makefile doc/fr/Makefile doc/nl/Makefile doc/pt/Makefile doc/sv/Makefile
    test/Makefile test/defs])
 AC_OUTPUT
PATCH
  patch -p0 <<'PATCH'
--- Makefile.am	2021-02-18 23:31:25.000000000 +0100
+++ -	2021-05-16 13:15:15.000000000 +0200
@@ -1,6 +1,6 @@
 AUTOMAKE_OPTIONS=foreign
 ACLOCAL_AMFLAGS = -I build-aux
-SUBDIRS=doc scripts test
+SUBDIRS=scripts test
 
 noinst_LTLIBRARIES = libcommunicate.la libmacosx.la
 libcommunicate_la_SOURCES = communicate.c
PATCH
}

build() {
  cd fakeroot-24d6b0857396cad87b2cabd32fb8af9ef4799915

  export LIBTOOLIZE=glibtoolize
  ./bootstrap
  ./configure --prefix=/opt/arch \
    --libdir=/opt/arch/lib/libfakeroot \
    --disable-static \
    --with-ipc=sysv \
    --disable-dependency-tracking \
    --disable-silent-rules

  make
}

package() {
  cd fakeroot-24d6b0857396cad87b2cabd32fb8af9ef4799915
  make DESTDIR="$pkgdir" install

  # install README for sysv/tcp usage
  ginstall -d -m755 "$pkgdir"/opt/arch/share/doc/$pkgname
  ginstall -m644 README "$pkgdir"/opt/arch/share/doc/$pkgname/README
}
