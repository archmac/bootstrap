pkgname=openssl
_ver=1.1.1k
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
options=('!makeflags')
backup=('opt/arch/etc/ssl/openssl.cnf')
install=openssl.install
source=(
  "https://www.openssl.org/source/${pkgname}-${_ver}.tar.gz"{,.asc}
  'ca-dir.patch'
  'openssl.install'
  'c_keychain'
)

prepare() {
    cd $srcdir/$pkgname-$_ver

    # set ca dir to /etc/ssl by default
    patch -p0 -i $srcdir/ca-dir.patch
}

build() {
    cd $srcdir/$pkgname-$_ver

    case $(uname -m) in
        x86_64)
            openssl_arch=darwin64-x86_64-cc
            ;;
        arm64)
            openssl_arch=darwin64-arm64-cc
            ;;
        *)
            false
            ;;
    esac

    perl ./Configure --prefix=/opt/arch --openssldir=/opt/arch/etc/ssl \
        no-ssl3 \
        no-ssl3-method \
        zlib-dynamic \
        shared \
        enable-cms \
        $openssl_arch \
        enable-ec_nistp_64_gcc_128

    make depend
    make
}

package() {
    cd $srcdir/$pkgname-$_ver
    make DESTDIR=$pkgdir MANDIR=/opt/arch/share/man MANSUFFIX=ssl install_sw install_ssldirs install_man_docs
    ginstall -m755 $srcdir/c_keychain $pkgdir/opt/arch/bin/c_keychain
    ginstall -d -m755 $pkgdir/opt/arch/share/licenses/$pkgname
    ginstall -m644 LICENSE $pkgdir/opt/arch/share/licenses/$pkgname/LICENSE
}
